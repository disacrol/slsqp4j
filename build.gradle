defaultTasks 'clean', 'build', 'test'

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0'
        classpath 'com.github.jengelman.gradle.plugins:shadow:5.0.0'
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'c'
    apply plugin: 'checkstyle'
    apply plugin: 'com.github.johnrengelman.shadow'
    apply plugin: 'org.junit.platform.gradle.plugin'
    apply plugin: 'jacoco'
    apply plugin: 'checkstyle'
    apply plugin: 'maven-publish'

    repositories {
        maven {
            url AWS_REPO_SNAPSHOT_URL
            credentials(AwsCredentials) {
                accessKey AWS_ACCESS_KEY
                secretKey AWS_SECRET_KEY
            }
        }
        mavenLocal()
        mavenCentral()
        jcenter()
    }

    checkstyle {
        toolVersion = checkstyleVersion
        configDir = file("$rootDir/config/checkstyle")
    }

    jacoco {
        toolVersion = '0.8.3'
        applyTo junitPlatformTest
    }

    junitPlatformTest {
        jacoco {
            destinationFile = file("$buildDir/jacoco/test.exec")
        }
    }

    jacocoTestReport {
        reports {
            xml.enabled false
            csv.enabled false
            html.destination file("${buildDir}/jacoco/html")
        }
    }

    jacocoTestCoverageVerification {
        violationRules {
            rule {
                limit {
                    minimum = 0.85
                }
            }
        }
    }

    sourceCompatibility = 1.8

    dependencies {
        checkstyle group: 'com.puppycrawl.tools', name: 'checkstyle', version: checkstyleVersion

        testCompile group: 'org.mockito', name: 'mockito-junit-jupiter', version: mockitoVersion
        testCompile group: 'org.junit.jupiter', name: 'junit-jupiter', version: junitVersion
    }
}
