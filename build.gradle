defaultTasks 'clean', 'build', 'test'

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0'
        classpath 'com.github.jengelman.gradle.plugins:shadow:5.0.0'
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'c'
    apply plugin: 'checkstyle'
    apply plugin: 'com.github.johnrengelman.shadow'
    apply plugin: 'org.junit.platform.gradle.plugin'
    apply plugin: 'checkstyle'
    apply plugin: 'maven-publish'
    apply plugin: 'com.bmuschko.nexus'

    archivesBaseName = 'slsqp4j'
    group = "com.skew.slsqp4j"
    version = platformVersion

    repositories {
        maven {
            url AWS_REPO_SNAPSHOT_URL
            credentials(AwsCredentials) {
                accessKey AWS_ACCESS_KEY
                secretKey AWS_SECRET_KEY
            }
        }
        mavenLocal()
        mavenCentral()
        jcenter()
    }

    publishing {
        repositories {
            maven {
                url AWS_REPO_SNAPSHOT_URL
                credentials(AwsCredentials) {
                    accessKey AWS_ACCESS_KEY
                    secretKey AWS_SECRET_KEY
                }
            }
        }
        publications {
            shadow(MavenPublication) { publication ->
                groupId platformGroupId
                artifactId project.name
                version platformVersion
                project.shadow.component(publication)
            }
            latest(MavenPublication) { publication ->
                groupId platformGroupId
                artifactId project.name
                version 'latest'
                project.shadow.component(publication)
            }
        }
    }

    modifyPom {
        project {
            name 'slsqp4j'
            description 'A JVM wrapper for the popular SLSQP optimizer'
            url 'https://github.com/skew-markets/slsqp4j'
            inceptionYear '2020'

            scm {
                url 'https://github.com/skew-markets/slsqp4j'
                connection 'scm:https://github.com/skew-markets/slsqp4j.git'
                developerConnection 'scm:git://github.com/skew-markets/slsqp4j.git'
            }

            licenses {
                license {
                    name 'BSD 3-Clause "New" or "Revised" License'
                    url 'https://spdx.org/licenses/BSD-3-Clause.html'
                }
            }

            developers {
                developer {
                    id 'jamesasefa'
                    name 'James Asefa'
                    email 'james.asefa@skew.com'
                }
            }
        }
    }

    extraArchive {
        sources = true
        javadoc = true
    }

    nexus {
        sign = true
        repositoryUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
        snapshotRepositoryUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
    }

    checkstyle {
        toolVersion = checkstyleVersion
        configDir = file("$rootDir/config/checkstyle")
    }

    sourceCompatibility = 1.8

    dependencies {
        checkstyle group: 'com.puppycrawl.tools', name: 'checkstyle', version: checkstyleVersion

        testCompile group: 'org.mockito', name: 'mockito-junit-jupiter', version: mockitoVersion
        testCompile group: 'org.junit.jupiter', name: 'junit-jupiter', version: junitVersion
    }
}
